plugins {
    id'java'
    id'application'
    id'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'de.netbeacon'
mainClassName = 'de.netbeacon.xenia.bot.core.Init'
sourceCompatibility = 16
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
    maven {
        name 'hypercdn-maven2'
        url 'https://nexus.hypercdn.de/repository/hypercdn-maven'
    }
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        name 'Jitpack.io'
        url 'https://jitpack.io'
    }
}

dependencies {

    // JDA

    implementation group: 'net.dv8tion', name: 'JDA', version: '4.3.0_309' // https://mvnrepository.com/artifact/net.dv8tion/JDA

    // JSON

    implementation group: 'org.json', name: 'json', version: '20210307' // https://mvnrepository.com/artifact/org.json/json

    // LOGGING

    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.32' // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.32' // https://mvnrepository.com/artifact/org.slf4j/slf4j-log4j12

    // CRYPT

    implementation group: 'org.mindrot', name: 'jbcrypt', version: '0.4' // https://mvnrepository.com/artifact/org.mindrot/jbcrypt
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.69' // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk15on
    implementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.69' // https://mvnrepository.com/artifact/org.bouncycastle/bcpkix-jdk15on

    // JS Engine (eval)

    implementation group: 'org.codehaus.groovy', name: 'groovy-jsr223', version: '3.0.8' // https://mvnrepository.com/artifact/org.codehaus.groovy/groovy-jsr223

    // OKHTTP

    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.1' // https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp

    // OTHER THINGS

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0' // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0' // https://mvnrepository.com/artifact/commons-io/commons-io
    implementation group: 'org.apache.tika', name: 'tika-core', version: '2.0.0' // https://mvnrepository.com/artifact/org.apache.tika/tika-core

    // clients
    implementation group: 'de.netbeacon.purrito', name: 'PurrBot-API-Wrapper', version: '2.0.5', changing: true // purrbot api wrapper
    implementation group: 'de.netbeacon.d43z1', name: 'D43Z1', version: '1.0.3', changing: true // d43z1
    implementation group: 'de.netbeacon.xenia.backend.client', name: 'Xenia-Backend-Client', version: '1.14.5v2', changing: true // backend client
    implementation group: 'de.netbeacon.utils', name: 'Logging-Utils', version: '1.0.0', changing: true // discord logging
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'hours'
}

processResources {
    //inject values into app.properties
    filesMatching("**/app.properties") {
        filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [
                "env.BUILD_VERSION"  : (System.getenv('CI') ? System.getenv('BUILD_VERSION') : '#.#.#'),
                "env.BUILD_NUMBER"  : (System.getenv('CI') ? System.getenv('BUILD_NUMBER') : 'Unofficial'),
                "env.BUILD_TIME"    : System.currentTimeMillis() + ''
        ]
    }
}